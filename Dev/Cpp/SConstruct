#!/usr/bin/env python
import os


def normalize_path(val, env):
    return val if os.path.isabs(val) else os.path.join(env.Dir("#").abspath, val)


def validate_parent_dir(key, val, env):
    if not os.path.isdir(normalize_path(os.path.dirname(val), env)):
        raise UserError("'%s' is not a directory: %s" % (key, os.path.dirname(val)))


libname = "effekseer"
projectdir = "../Godot"

localEnv = Environment(tools=["default"], PLATFORM="")

customs = ["custom.py"]
customs = [os.path.abspath(path) for path in customs]

opts = Variables(customs, ARGUMENTS)
opts.Add(
    BoolVariable(
        key="compiledb",
        help="Generate compilation DB (`compile_commands.json`) for external tools",
        default=localEnv.get("compiledb", False),
    )
)
opts.Add(
    PathVariable(
        key="compiledb_file",
        help="Path to a custom `compile_commands.json` file",
        default=localEnv.get("compiledb_file", "compile_commands.json"),
        validator=validate_parent_dir,
    )
)
opts.Update(localEnv)

Help(opts.GenerateHelpText(localEnv))

env = localEnv.Clone()
env["compiledb"] = False

env.Tool("compilation_db")
compilation_db = env.CompilationDatabase(
    normalize_path(localEnv["compiledb_file"], localEnv)
)
env.Alias("compiledb", compilation_db)

env = SConscript("godot-cpp/SConstruct", {"env": env, "customs": customs})

env.Append(CPPPATH=[
    "../Effekseer/Dev/Cpp/Effekseer",
    "../Effekseer/Dev/Cpp/EffekseerRendererCommon"
])

sources = []

## Effekseer
sources += Glob("../Effekseer/Dev/Cpp/Effekseer/Effekseer/*.cpp")
sources += Glob("../Effekseer/Dev/Cpp/Effekseer/Effekseer/**/*.cpp")
sources += Glob("../Effekseer/Dev/Cpp/EffekseerRendererCommon/*.cpp")

## EffekseerGodot4
sources += Glob("src/*.cpp")
sources += Glob("src/LoaderGodot/*.cpp")
sources += Glob("src/RendererGodot/*.cpp")
sources += Glob("src/RendererGodot/Shaders/*.cpp")
sources += Glob("src/SoundGodot/*.cpp")
sources += Glob("src/Utils/*.cpp")


libfile = f"lib{libname}.{env['arch']}{env['SHLIBSUFFIX']}"

# if env["platform"] == "macos" or env["platform"] == "ios":
#     libfile = f"{libname}.framework/{env['platform']}/{libname}"

library = env.SharedLibrary(
    f"bin/{env['platform']}/{libfile}",
    source=sources,
)

copy = env.InstallAs(
    f"{projectdir}/addons/{libname}/bin/{env['platform']}/{libfile}", 
    library
)

default_args = [library, copy]
if localEnv.get("compiledb", False):
    default_args += [compilation_db]
Default(*default_args)